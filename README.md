# Создание и настройка базы

Источник: курс от Нетология "1С-программист: первые шаги в профессию" https://netology.ru/profile/program/onecfree-33/schedule

1. Создайте новую информационную базу и откройте ее в режиме Конфигуратор.
    
2. Установите свойства корня конфигурации:
    
    - Имя: ЛичныеФинансы
    - Синоним: Личные финансы
    - Авторские права: Ваша фамилия и имя
    - Версия: 0.0.0.1

## Создание справочников, документов и реквизитов

1. Создайте в группе Общие новый Определяемый тип ДенежнаяСумма, с типом Число (15, 2)

![2025-02-15_13-08-45](https://github.com/user-attachments/assets/99fbb374-1393-4ffc-8957-dc469d8d3af4)
    
2. Создайте перечисление **ВидыЛичныхСчетов** со значениями Наличные, Банк

![2025-02-15_13-36-25](https://github.com/user-attachments/assets/65f78f90-01ca-421a-a974-7a967234d8e7)
    
3. Создайте справочник **Валюты** с предопределенным элементом Рубль

![2025-02-15_13-40-02](https://github.com/user-attachments/assets/8bc33e73-537c-483f-86da-06ec269add1d)

4. Создайте справочник **ЛичныеСчета** с длиной наименования 150 и реквизитами:
    
    - ВидСчета - ПеречислениеСсылка.ВидыЛичныхСчетов

		![2025-02-15_13-44-08 1](https://github.com/user-attachments/assets/0c4fa053-a0f1-43cb-9f76-582fb327fa89)

    - Валюта - СправочникСсылка.Валюты

		![2025-02-15_14-03-06](https://github.com/user-attachments/assets/615b7164-6eb4-48ef-a137-d678c1dbc1a3)

5. Создайте справочник **СтатьиДвиженийДенежныхСредств** с длиной наименования 150 и включенной иерархией элементов. Создайте предопределенные элементы на верхнем уровне иерархии на свое усмотрение.

![2025-02-15_14-05-07](https://github.com/user-attachments/assets/0c22a592-6e91-4751-8e2e-02cc1279494a)
![2025-02-15_14-06-09](https://github.com/user-attachments/assets/563406f8-93dc-423f-b64e-486a3c6bd9ec)

6. Создайте справочник **Номенклатура** с длиной наименования 150 и включенной иерархией групп и элементов.

![2025-02-15_14-09-02](https://github.com/user-attachments/assets/383ad52f-3dbb-47cc-9a20-f253f2507e29)
![2025-02-15_14-09-29](https://github.com/user-attachments/assets/b3fe7d48-4dec-4f76-9c16-c6fcdd280a13)
    
7. Создайте документ **ПолучениеДохода** с реквизитами:


    - Счет - СправочникСсылка.ЛичныеСчета
    ![2025-02-15_14-12-04](https://github.com/user-attachments/assets/4fe064dd-e7bc-4b25-b9ec-fe9f2e201e5c)

    - СтатьяДохода - СправочникСсылка.СтатьиДвиженияДенежныхСредств
    ![2025-02-15_14-13-09](https://github.com/user-attachments/assets/a6d0cb6d-4512-47e4-94df-147e4f34fb72)

    - Сумма - ОпределяемыйТип.ДенежнаяСумма
    ![2025-02-15_14-15-24](https://github.com/user-attachments/assets/7a704fc0-d987-46f3-b944-d1fe425f4e50)

    - Комментарий - Строка неограниченной длины
    ![2025-02-15_14-16-16](https://github.com/user-attachments/assets/315649cb-e122-48c6-ab7f-ce06e665f2aa)


8. Создайте документ **ПереводМеждуСчетами** с реквизитами:
    
    - СчетСписания - СправочникСсылка.ЛичныеСчета
    ![2025-02-15_14-17-50](https://github.com/user-attachments/assets/d6b0d2f7-f86f-4a2d-bab4-4a37cbdbed2c)

    - СчетЗачисления - СправочникСсылка.ЛичныеСчета
    ![2025-02-15_14-18-53](https://github.com/user-attachments/assets/cedcd186-a586-4e8a-a588-bbe27b20beae)

    - Сумма - ОпределяемыйТип.ДенежнаяСумма
    ![2025-02-15_14-19-42](https://github.com/user-attachments/assets/d9c42a34-ed85-4f62-b9c4-b2427a09e184)

    - Комментарий - Строка неограниченной длины
	![2025-02-15_14-20-45](https://github.com/user-attachments/assets/6820a560-32fd-4cd8-8a29-d58cdc4ac1a8)


9. Создайте документ **Покупка** с реквизитами:
    
    - Счет - СправочникСсылка.ЛичныеСчета
    ![2025-02-15_14-21-51](https://github.com/user-attachments/assets/78ae0306-52b7-433f-901c-e5d3b3b4bb83)

    - СтатьяРасхода - СправочникСсылка.СтатьиДвиженияДенежныхСредств
    ![2025-02-15_14-22-46](https://github.com/user-attachments/assets/75cab8bd-2ec7-4736-bb6d-4b44fe1c234d)

    - РазделитьПоТоварам - Булево
    ![2025-02-15_14-23-25](https://github.com/user-attachments/assets/f729c7a9-6880-4ac5-b4f8-1b0e21c60754)

    - Сумма - ОпределяемыйТип.ДенежнаяСумма
    ![2025-02-15_14-24-11](https://github.com/user-attachments/assets/f7d1b6b4-085d-4bec-8f90-035c64d38c3e)

    - Комментарий - Строка неограниченной длины
    ![2025-02-15_14-24-54](https://github.com/user-attachments/assets/b5e9050b-4aaa-405e-a029-ac9aa7ee64eb)

    - Табличной частью ТоварыУслуги с колонками:
        - Номенклатура - СправочникСсылка.Номенклатура
        ![2025-02-15_14-26-37](https://github.com/user-attachments/assets/b8c22411-87e0-41d5-8463-6b69c2e53a03)

        - Количество - Число (10, 0)
        ![2025-02-15_14-27-49](https://github.com/user-attachments/assets/b7681cad-98fd-4941-b828-d4a28ac7edb7)

        - Цена - ОпределяемыйТип.ДенежнаяСумма
        ![2025-02-15_14-28-35](https://github.com/user-attachments/assets/7cf6bfb8-b3f0-4770-ba07-2b1d4de32f4d)

        - Сумма - ОпределяемыйТип.ДенежнаяСумма
        ![2025-02-15_14-29-24](https://github.com/user-attachments/assets/ee1eeb0c-83f6-48b2-b36c-13ebdcf732ef)


## Настройка интерфейса

1. В группе Общие создайте подсистемы:
    
    - Финансы, с составом:
        - Документ ПолучениеДохода
        - Документ ПереводМеждуСчетами
        - Документ Покупка
        ![2025-02-15_14-30-46](https://github.com/user-attachments/assets/c613d84e-93eb-4df8-9495-69cc513b4c1f)

    - Настройки, с составом:
        - Справочник Валюты
        - Справочник ЛичныеСчета
        - Справочник СтатьиДвиженийДенежныхСредств
        - Справочник Номенклатура
        ![2025-02-15_14-31-24](https://github.com/user-attachments/assets/39d5dec1-8df2-47ab-83d1-6fd1109f3d6d)

2. В корне конфигурации откройте свойство Интерфейс клиентского приложения, разместите панели так, как на ваш взгляд оптимальнее для работы с приложением. Можно запустить приложение, посмотреть получившийся результат и перенастроить.

	![2025-02-15_14-32-48](https://github.com/user-attachments/assets/07b456d5-91b9-4c61-8f20-b57818d815b1)
	
    
3. В группе Общие создайте новый стиль, настройте для стиля цветовое оформление приложения на свой вкус. В корне конфигурации установите новый стиль в свойстве Основной стиль.
    ![2025-02-15_14-53-16](https://github.com/user-attachments/assets/e34f9a8b-9b57-403d-b9d3-b8ed448ea0a5)


## Создание и настройка формы документа

1. Создайте форму документа для документа Покупка

![2025-02-18_12-25-51](https://github.com/user-attachments/assets/3b6ffe36-a300-401b-8b05-2877da3ed829)

    
2. Реализуйте в форме документа процедуру для управления видимостью элементов УправлениеВидимостьюЭлементовФормы с директивой компиляции &НаКлиентеНаСервереБезКонтекста.

Примерный код процедуры
```c
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюЭлементовФормы(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	РазделитьПоТоварам = Объект.РазделитьПоТоварам;
	
	Элементы.ТоварыУслуги.Видимость = РазделитьПоТоварам;
	Элементы.Сумма.ТолькоПросмотр = РазделитьПоТоварам;
	
КонецПроцедуры
```

![2025-02-18_12-27-47](https://github.com/user-attachments/assets/3ec87cf7-d7e3-4893-bcf6-9f9b035a03cd)


3. Для формы назначьте обработчик события ПриСозданииНаСервере. В обработчике вызовите ранее созданную процедуру `УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);`

![2025-02-18_12-30-32](https://github.com/user-attachments/assets/1ccd5cbf-aa43-4e5e-91c3-5b9e3d3af5d2)


```c
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);
КонецПроцедуры
```

4. Для элемента **РазделитьПоТоварам** назначте обработчик события ПриИзменении. В обработчике вызовите ранее созданную процедуру `УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);`

- зайти в раздел "Форма"
- правой клавишей по "РазделитьПоТоварам" 
- "События"
- выбрать <ПриИзменении>
- нажать "ОК"
- в разделе "Модуль" отобразится часть кода
- к нему добавить строку "`УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);`"

```c
&НаКлиенте
Процедура РазделитьПоТоварамПриИзменении(Элемент)
	УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);
КонецПроцедуры
```

![2025-02-18_13-00-44](https://github.com/user-attachments/assets/25970f10-3256-4ada-87b3-176f450852be)

    
5. Реализуйте в документе процедуру для расчета суммы документа РассчитатьИтогДокумента с директивой компиляции &НаКлиенте.

Примерный код процедуры
```c
&НаКлиенте
Процедура РассчитатьИтогДокумента()
	
	Объект.Сумма = Объект.ТоварыУслуги.Итог("Сумма");
	
КонецПроцедуры
```

6. Реализуйте в документе процедуру для расчета стоимость в строке табличной части и последующего пересчета суммы документа РассчитатьСуммуСтроки с директивой компиляции &НаКлиенте.

Примерный код процедуры
```c
&НаКлиенте
Процедура РассчитатьСуммуСтроки(Строка)
	
	Строка.Сумма = Строка.Цена * Строка.Количество;
	
	РассчитатьИтогДокумента();
	
КонецПроцедуры
```

7. Для элементов НоменклатураКоличество и НоменклатураЦена назначте обработчики события ПриИзменении. В обработчике вызовите ранее созданную процедуру с передачей текущей строки:

```c
&НаКлиенте
Процедура НоменклатураКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТоварыУслуги.ТекущиеДанные;
	РассчитатьСуммуСтроки(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураЦенаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТоварыУслуги.ТекущиеДанные;
	РассчитатьСуммуСтроки(ТекущиеДанные);
КонецПроцедуры

```
# Итог всего кода "Модуль" документа "Покупка":

```c
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеВидимостьюЭлементовФормы(Форма)
	
	Элементы = Форма.Элементы;
	Объект = Форма.Объект;
	
	РазделитьПоТоварам = Объект.РазделитьПоТоварам;
	
	Элементы.ТоварыУслуги.Видимость = РазделитьПоТоварам;
	Элементы.Сумма.ТолькоПросмотр = РазделитьПоТоварам;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РазделитьПоТоварамПриИзменении(Элемент)
	УправлениеВидимостьюЭлементовФормы(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьИтогДокумента()
	
	Объект.Сумма = Объект.ТоварыУслуги.Итог("Сумма");
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуСтроки(Строка)
	
	Строка.Сумма = Строка.Цена * Строка.Количество;
	
	РассчитатьИтогДокумента();
	
КонецПроцедуры  

&НаКлиенте
Процедура НоменклатураКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТоварыУслуги.ТекущиеДанные;
	РассчитатьСуммуСтроки(ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураЦенаПриИзменении(Элемент)
	ТекущиеДанные = Элементы.ТоварыУслуги.ТекущиеДанные;
	РассчитатьСуммуСтроки(ТекущиеДанные);
КонецПроцедуры

```
## Создание регистра сведений для хранения курсов валют

1. Создайте регистр сведений **КурсыВалют**. Периодичность - День.

![2025-02-18_13-15-37](https://github.com/user-attachments/assets/2b89c539-bc20-404c-82d4-75b287b20c46)

- "Регистры сведений" -- правой кнопкой -- "Добавить" -- в поле "Имя" ввести "КурсыВалют" -- кнопка "Tab"
- в поле "Периодичность" выбираем "В пределах дня"

![2025-02-18_13-18-52](https://github.com/user-attachments/assets/ddecbbcd-d914-482c-bd42-ef0a3f88ce38)

 
- вкладка "Данные"
	- выделить "Измерения" -- нажать на зеленый плюс "Добавить" 
		- в поле "Имя" ввести "Валюта"
		- в поле "Тип" выбрать "СправочникСсылка.Валюты"

	![2025-02-18_13-21-45](https://github.com/user-attachments/assets/ceea8f0d-d98b-40e6-af64-0c468c798138)


	- выделить "Ресурсы" -- нажать на зеленый плюс "Добавить" 
		- в поле "Имя" ввести "Кратность"
		- в поле "Тип" выбрать "Число"

	![2025-02-18_13-27-37](https://github.com/user-attachments/assets/7d4b6464-5bfe-4cc5-9cb8-aeb9d8aaf4e2)


	 - выделить "Ресурсы" -- нажать на зеленый плюс "Добавить" 
		- в поле "Имя" ввести "Курс"
		- в поле "Тип" выбрать "Число"
		- в поле "Длина" ввести "15"
		- в поле "Точность ввести "4"
    
	![2025-02-18_13-29-33](https://github.com/user-attachments/assets/a5badd5e-4e02-4588-9730-97027cba775d)


2. Включите регистр сведений в одну из созданных подсистем
- вкладка "Подсистемы" -- поставить галочку в любой подсистеме

    ![2025-02-18_13-20-32](https://github.com/user-attachments/assets/699c3b48-4b64-4080-96c6-b9f4e91b9ef5)


## Загрузка курсов валют

1. Создайте обработку **ЗагрузкаКурсаВалюты**

	- правой кнопкой по "Обработки"
	- нажать "Добавить"
	- в поле "Имя" ввести "ЗагрузкаКурсаВалюты"
	- нажать кнопку "Tab"
	![2025-02-18_13-33-37](https://github.com/user-attachments/assets/291efc7c-a0d1-4ea3-8a0e-4511818fcebd)


2. Включите обработку в одну из созданных подсистем

	- вкладка "Подсистемы"
	- поставить галочку "Финансы"
	![2025-02-18_13-34-04](https://github.com/user-attachments/assets/ab09963d-d53e-4481-8f1d-0f27fad17b7e)


3. Создайте форму обработки

	- раскрыть список "ЗагрузкаКурсаВалюты"
	- правой кнопкой по "Формы"
	- нажать "Добавить"
	![2025-02-18_13-35-24](https://github.com/user-attachments/assets/9ffda0b5-7fe0-4a3a-bd6e-46190820fbce)

	- кнопка "Готово"
	![2025-02-18_13-36-34](https://github.com/user-attachments/assets/6ca88dc2-e3a2-4c23-b8fe-3c1484a269d7)


4. Создайте команду на форме обработки, вынесите на командную панель, назначьте кнопкой по умолчанию, создайте обработчик на клиенте и на сервере

	- Во вкладке "Команда формы" нажать зеленый плюс "Добавить"
	![2025-02-18_13-38-55](https://github.com/user-attachments/assets/a957173f-7923-4b1e-8a7d-255613552020)

	- перетащить "Кнопка1" в окно с формой в "Командная панель"
	![2025-02-18_13-57-59](https://github.com/user-attachments/assets/074d7516-7ab1-4f67-9246-bbef28b67780)

	- дважды щелкнуть по "ФормаКоманда1"
	- в чек-боксе "КнопкаПоУмолчанию" -- поставить галочку
	![2025-02-18_13-58-55](https://github.com/user-attachments/assets/c9810d02-954b-49d6-a8df-ca1f3bfcfa92)

	- правой кнопкой по "ФормаКоманда1" -- нажать <Действия команды>
	![2025-02-18_14-01-46](https://github.com/user-attachments/assets/9765f5dc-c77f-4203-a699-5f75e3a839ee)

	- выбрать "Создать на клиенте и процедуру на сервере" -- нажать кнопку "ОК"
	![2025-02-18_14-04-48](https://github.com/user-attachments/assets/aecc27ef-a6fe-404b-99bd-4ec18e8a0461)

	- результат в "Модуль"
	```c
	&НаКлиенте
	Процедура Команда1(Команда)
		Команда1НаСервере();
	КонецПроцедуры
	
	&НаСервере
	Процедура Команда1НаСервере()
		// Вставить содержимое обработчика.
	КонецПроцедуры
	```

5. В процедуру на клиенте после вызова сервера добавьте строчку для оповещения пользователя об успешной загрузке `ПоказатьОповещениеПользователя("Курсы валют загружены");`

	```c
	&НаКлиенте
	Процедура Команда1(Команда)
		Команда1НаСервере();
		ПоказатьОповещениеПользователя("Курсы валют загружены");
	КонецПроцедуры
	```

6. В процедуру на сервере добавьте последовательный вызов процедур по получению данных с сайта и записи полученных данных в базу:
    
	```c
	&НаСервере
	Процедура Команда1НаСервере()
		ДанныеСайта = ПолучитьДанныеССайта();
		ЗаписатьКурсыВалют(ДанныеСайта);
	КонецПроцедуры
	```

7. Реализуйте алгоритм процедуры по получению данных с сайта и записи полученных данных в базу:

	Пример кода для решения задачи
	```c
	&НаСервере
	Функция ПолучитьДанныеССайта()
		
		Соединение = Новый HTTPСоединение("www.cbr-xml-daily.ru", 443,,,,, Новый ЗащищенноеСоединениеOpenSSL);
		
		Запрос = Новый HTTPЗапрос("/daily_utf8.xml");
		
		Ответ = Соединение.Получить(Запрос);
		
		Если Ответ.КодСостояния <> 200 Тогда
			ВызватьИсключение "Ошибка соединения с сайтом ЦБ";
		КонецЕсли;
		
		Возврат Ответ.ПолучитьТелоКакСтроку();
		
	КонецФункции
	
	&НаСервере
	Процедура ЗаписатьКурсыВалют(ДанныеСайта)
		
		Чтение = Новый ЧтениеXML;
		Чтение.УстановитьСтроку(ДанныеСайта);
		
		Построитель = Новый ПостроительDOM;
		Документ = Построитель.Прочитать(Чтение);
		
		Для Каждого Узел Из Документ.ЭлементДокумента.ДочерниеУзлы Цикл
			
			Кратность = 1;
			Курс = 1;
			СимвольныйКод = "";
			
			Для Каждого СвойствоВалюты Из Узел.ДочерниеУзлы Цикл
				Если СвойствоВалюты.ИмяУзла = "CharCode" Тогда
					СимвольныйКод = СвойствоВалюты.ТекстовоеСодержимое;
				КонецЕсли;
				Если СвойствоВалюты.ИмяУзла = "Nominal" Тогда
					Кратность = Число(СвойствоВалюты.ТекстовоеСодержимое);
				КонецЕсли;
				Если СвойствоВалюты.ИмяУзла = "Value" Тогда
					Курс = Число(СвойствоВалюты.ТекстовоеСодержимое);
				КонецЕсли;
			КонецЦикла;
			
			Валюта = Справочники.Валюты.НайтиПоКоду(СимвольныйКод);
			
			Если Не ЗначениеЗаполнено(Валюта) Тогда
				Продолжить;
			КонецЕсли;		
			
			Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
			Запись.Период = ТекущаяДата();
			Запись.Валюта = Валюта;
			Запись.Кратность = Кратность;
			Запись.Курс = Курс;
			Запись.Записать();
			
		КонецЦикла;	
		
	КонецПроцедуры
	```
